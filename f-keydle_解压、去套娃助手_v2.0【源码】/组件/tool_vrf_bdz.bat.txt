@echo off
rem -------------------- encoding:GB 2312(ANSI) ---------------------
rem It is recommended to use "Visual Studio Code" to edit this file.

setlocal enabledelayedexpansion
title ｜子执行器｜
cd /d "%~dp0"

rem 根据系统是64位还是32位准备兼容的bandizip。统一复制成“Bandizip.exe”然后在后面需要时启动，这对压缩失败检测工具根据进程名称锁定窗口有帮助。
rem 下面两个MD5的值如果升级bandizip，也需要更改。【bandizip_x64_md5】对应“Bandizip.x64.exe”，【bandizip_x86_md5】对应“Bandizip.x86.exe”
set bandizip_x64_md5=b7e454b08d5a0d0e8856e716dde7bd33
set bandizip_x86_md5=19e75f850a90e65a96d78f40444dc319
if exist "%windir%\SysWOW64" (
    rem 检查是否存在bandizip
    if exist "BandizipPro_portable\Bandizip.exe" (
        rem 如果存在，检验是不是64位的。
        certutil -hashfile "BandizipPro_portable\Bandizip.exe" md5 | find "!bandizip_x64_md5!"
        rem 如果不是，就需要覆盖；是的话当然什么也不用做
        if !errorlevel! NEQ 0 (
            rem 需要先删除，如果直接copy会访问失败。
            del /f /q "BandizipPro_portable\Bandizip.exe"
            rem 先尝试硬链接（少占空间），如果失败，换拷贝。
            mklink /h "BandizipPro_portable\Bandizip.exe" "BandizipPro_portable\Bandizip.x64.exe" >nul
            if not exist "BandizipPro_portable\Bandizip.exe" (
                copy /y "BandizipPro_portable\Bandizip.x64.exe" "BandizipPro_portable\Bandizip.exe" >nul
            )
        )
    rem 不存在就复制一份
    ) else (
        rem 先尝试硬链接（少占空间），如果失败，换拷贝。
        mklink /h "BandizipPro_portable\Bandizip.exe" "BandizipPro_portable\Bandizip.x64.exe" >nul
        if not exist "BandizipPro_portable\Bandizip.exe" (
            copy /y "BandizipPro_portable\Bandizip.x64.exe" "BandizipPro_portable\Bandizip.exe" >nul
        )
    )
) else (
    rem 检查是否存在bandizip
    if exist "BandizipPro_portable\Bandizip.exe" (
        rem 如果存在，检验是不是32位的。
        certutil -hashfile "BandizipPro_portable\Bandizip.exe" md5 | find "!bandizip_x86_md5!"
        rem 如果不是，就需要覆盖；是的话当然什么也不用做
        if !errorlevel! NEQ 0 (
            rem 需要先删除，如果直接copy会访问失败。
            del /f /q "BandizipPro_portable\Bandizip.exe"
            rem 先尝试硬链接（少占空间），如果失败，换拷贝。
            mklink /h "BandizipPro_portable\Bandizip.exe" "BandizipPro_portable\Bandizip.x86.exe" >nul
            if not exist "BandizipPro_portable\Bandizip.exe" (
                copy /y "BandizipPro_portable\Bandizip.x86.exe" "BandizipPro_portable\Bandizip.exe" >nul
            )
        )
    rem 不存在就复制一份
    ) else (
        rem 先尝试硬链接（少占空间），如果失败，换拷贝。
        mklink /h "BandizipPro_portable\Bandizip.exe" "BandizipPro_portable\Bandizip.x86.exe" >nul
        if not exist "BandizipPro_portable\Bandizip.exe" (
            copy /y "BandizipPro_portable\Bandizip.x86.exe" "BandizipPro_portable\Bandizip.exe" >nul
        )
    )
)

exit