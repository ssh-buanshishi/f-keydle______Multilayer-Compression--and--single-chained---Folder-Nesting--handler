@echo off
rem -------------------- encoding:GB 2312(ANSI) ---------------------
rem It is recommended to use "Visual Studio Code" to edit this file.

setlocal enabledelayedexpansion
title 05-修复文件夹读写权限

rem 下面这行看似无意义的命令不是闲着没事写的，是为了防止系统找不到“北”，win10没有这行可能不会有任何问题，
rem 但是一位朋友的win11因为没了这行，配置bandizip版本时找不到当前目录下的bandizip文件在哪，我一开始也
rem 摸不着头脑为什么会找不到bandizip，后来加了这条命令后问题就自己消失了，不得不说这也太玄幻了。
cd /d "%~dp0"

rem win10的attrib支持仅有“/D”，而win7的attrib若要处理文件夹，“/S”和“/D”要组合着一起使用
rem "/S"是递归，处理量比仅有“/D”大很多，而文件夹必须解除隐藏状态才能被批处理识别，因此如果是win10
rem 及以上的话（win11的NT版本据说也是“10.”开头的），有必要加速一下。
ver | find "10." >nul
if %errorlevel% EQU 0 (
    set /a NT_10_up=1
) else (
    set /a NT_10_up=0
)

rem 存入当前路径
set origin_path=%~dp0
rem 路径末尾可能会自带“\”，下面去掉它。
set t_m_p=!origin_path:~-1!
if "%t_m_p%"=="\" (set origin_path=!origin_path:~0,-1!)


rem 检查是否传过来了参数（传了参数：mode == 1，拖曳文件的方法；没传参数：mode == 0，手动设置目标的方法）
set /a mode=0

set input=%~1

if defined input (
    rem 由于“mshta vbs...”命令的限制，不能有空格，所以传过来时空格被替换，这里还原回来。
    set target_path=!input:【_空格_】= !

    set /a mode=1
)

if !mode! EQU 1 (goto check) else (goto display_info)


:display_info
echo 功能：尝试修复没有读写、删除权限或者对用户拒绝访问的文件夹和其中的内容，
echo      获取到目标文件夹完全的访问权限。不一定百分百成功。
echo.
echo      “ 01-解多层压缩+解文件夹套娃.bat ” 和 “ 02-【仅】解文件夹套娃.bat ”
echo      运行时会自动完成上述获取权限的操作，这个工具单独拿出来是为了用户
echo      日常单独解决此类问题的，在非系统文件夹下碰到因为权限问题无法访问、删除
echo      的文件夹和其中的文件时，可以尝试运行这个批处理工具。
echo.
echo      【注意！！不可用于系统文件夹！！】
echo.
echo.
goto receive


:receive
echo 请将需要处理的【文件】或【文件夹】拖入窗口（接收目标路径），之后按回车。
echo 如果需要退出，确保看到此消息后直接按回车或关闭窗口，不要拖入文件夹或输入任何内容。
set input=<nul
set /p input=
if not defined input (exit)

rem 统一去掉可能存在的两侧的双引号
set target_path=%input:"=%
rem 路径末尾可能会自带“\”，下面去掉它。
set t_m_p=!target_path:~-1!
if "%t_m_p%"=="\" (set target_path=!target_path:~0,-1!)
goto check



:check
cls
echo 正在处理中，请耐心等待……

rem 为了扫清权限障碍，下面再查看路径是否存在就稳一点
cd /d "%origin_path%"
start "｜子执行器｜" /min /wait tool_gp.bat 0 "%target_path%"

if exist "!target_path!" (
    goto process
) else (
    rem 发送路径不存在的提醒
    cls
    cd /d "%origin_path%"
    start "title" _通知_.exe 1
    if !mode! EQU 1 (exit) else (goto receive)
)


:process
rem 尝试cd操作目标路径，操作失败的就是文件
cd /d "%target_path%" 2>nul
if !errorlevel! EQU 0 (
    cd ".."
    rem 确认是文件夹，替换为能够向下继承的权限
    cd /d "%origin_path%"
    start "｜子执行器｜" /min /wait tool_gp.bat 1 "%target_path%"
) else (
    rem 由于是单个文件，上面已经处理完了，所以结束
    cls
    cd /d "%origin_path%"
    rem 发送结束通知
    start "title" _通知_.exe 0

    if !mode! EQU 1 (exit) else (goto receive)
)


cd /d "%origin_path%"
rem 开始往这个文件夹中“塞”文件
rem 可能是不同盘符，这种情况不能用硬链接。
if not exist "!target_path!\_child_excuter_5.bat" (copy /y "05_fix_rw_permission_child.bat" "!target_path!\_child_excuter_5.bat" >nul)


cd /d "%target_path%"
rem 启动刚刚复制过来的“子”执行器
start "｜子执行器｜" /min /wait _child_excuter_5.bat %NT_10_up%
rem 这里不需要检测返回值


cd /d "%target_path%"
rem 执行清理工作
del /f /s /q "_child_excuter_5.bat" >nul




cls
cd /d "%origin_path%"
rem 发送结束通知
start "title" _通知_.exe 0

if !mode! EQU 1 (exit) else (goto receive)
